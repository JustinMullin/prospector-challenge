version = '0.1'

ext {
    appName = "prospector-challenge"
    gdxVersion = '1.9.6'
    kotlinVersion = '1.2.10'
}

buildscript {
    ext {
        kotlinVersion = '1.2.10'
        clojurePluginVersion = '0.3.0'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "http://clojars.org/repo" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "io.github.gradle-clojure:gradle-clojure-plugin:$clojurePluginVersion"
    }
}

import static org.apache.tools.ant.taskdefs.condition.Os.FAMILY_MAC
import static org.apache.tools.ant.taskdefs.condition.Os.isFamily

apply plugin: "kotlin"
apply plugin: "application"
apply plugin: "idea"
apply plugin: "scala"
apply plugin: "gradle-clojure.clojure"

compileScala {
    classpath = classpath.plus(files(compileKotlin.destinationDir))
    dependsOn compileKotlin
}
compileJava {
    classpath = classpath.plus(files(compileKotlin.destinationDir))
    dependsOn compileKotlin
}
compileClojure {
    classpath += files(compileKotlin.destinationDir)
    options.aotCompile = true
    dependsOn compileKotlin
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
}

applicationDefaultJvmArgs = isFamily(FAMILY_MAC) ? ["-XstartOnFirstThread"] : []
mainClassName = "xyz.jmullin.prospector.ProspectorKt"

task pack(type: JavaExec) {
    workingDir = "src/main/resources/"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs applicationDefaultJvmArgs
    main = "xyz.jmullin.prospector.utility.PackKt"
}

task runHeadless(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    args "headless"
    jvmArgs applicationDefaultJvmArgs
    main = mainClassName
}

dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    compile "org.reflections:reflections:0.9.11"
    compile "xyz.jmullin:drifter:0.1"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.19"
    compile 'org.scala-lang:scala-library:2.12.4'
    compile 'org.clojure:clojure:1.8.0'
}